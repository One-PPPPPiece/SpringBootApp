/**
 * The build script block is where you configure the repositories and dependencies
 * for Gradle itself, you should not include dependencies for your modules here.
 * For example, this block includes the spring-boot-gradle-plugin for
 * Gradle as a dependency because it provides the additional instructions Gradle
 * needs to build spring boot app modules.
 */
buildscript {
	ext {
		projectVersion = '1.0.0'
		javaVersion = '14.0.1'
		flywayVersion = '5.2.4'
		lombokVersion = '1.18.8'
		springDependencyManagementVersion = '1.0.9.RELEASE'
		springBootVersion = '2.3.1.RELEASE'
		googleJavaFormatVersion = '0.8'
	}
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementVersion"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

plugins {
	id 'org.springframework.boot' version "$springBootVersion"
	id "com.github.sherter.google-java-format" version "$googleJavaFormatVersion"
	id "jacoco"
}

repositories {
	mavenCentral()
}

/**
 * The sub-projects block is where you configure the repositories and dependencies
 * used by all modules in your project, such as third-party plugins or libraries.
 * However, you should configure module-specific dependencies in each module-level
 * build.gradle file.
 */
subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'
	apply plugin: "com.github.sherter.google-java-format"

	group 'com.ljh516.entertainment'
	version = "$projectVersion"
	sourceCompatibility = "$javaVersion"

	build.dependsOn "googleJavaFormat"

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	repositories {
		maven { url "https://repo.spring.io/libs-release" }
		maven { url "https://repo.spring.io/libs-milestone"}
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation "org.flywaydb:flyway-core:$flywayVersion"
		implementation 'io.springfox:springfox-swagger2:2.9.2'
		implementation 'io.springfox:springfox-swagger-ui:2.9.2'
		implementation 'javax.persistence:javax.persistence-api:2.2'

		compile 'org.postgresql:postgresql'

		compileOnly "org.projectlombok:lombok:$lombokVersion"
		annotationProcessor "org.projectlombok:lombok:$lombokVersion"
		testCompileOnly "org.projectlombok:lombok:$lombokVersion"
		testImplementation "org.meanbean:meanbean:2.0.3"
		testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
	}

	googleJavaFormat {
		options style: 'AOSP'
	}

	jacocoTestReport {
		reports {
			xml.enabled true
			html.enabled true
			csv.enabled false
		}
	}
	test.finalizedBy jacocoTestReport
}
